buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }

    dependencies {
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
    }
}

plugins {
    id 'java'
}

apply plugin: 'maven'
apply plugin: 'forge'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "${minecraftVersion}-${forgeVersion}"
    mappings = "stable_12"
}

repositories {
    mavenCentral()

    maven {
        url 'https://repo.spongepowered.org/maven/'
    }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation files('common-5.4-SNAPSHOT')
    compileOnly files('loader-utils-5.4-SNAPSHOT')
    implementation project(':forge-api')

    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }
}

ext.mixinSrg = new File(project.buildDir, "resources/main/stoneluckperms.mixins.json")
ext.mixinRefMap = new File(project.buildDir, "resources/main/stoneluckperms.refmap.json")

build {
    dependsOn(":build")
}

jar {
    manifest {
        attributes(
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Implementation-Title': 'LuckPerms',
                'Implementation-Vendor': 'LuckPerms',
                'Implementation-Version': project.ext.fullVersion,
                'Specification-Title': 'luckperms',
                'Specification-Vendor': 'LuckPerms',
                'Specification-Version': '1'
        )
    }
}

processResources {
    filesMatching('mcmod.info') {
        expand 'version': project.ext.fullVersion
    }
}

reobf {
    shadowJar {}
}

shadowJar {
    archiveName = "LuckPerms-Forge-${project.ext.fullVersion}.jar"

    from(rootProject.buildDir.path + "/libs") {
        include '*.jarinjar'
    }

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
    }
}

artifacts {
    archives shadowJar
}
