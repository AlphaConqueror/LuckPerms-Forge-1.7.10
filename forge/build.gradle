buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
    }
}

apply plugin: 'maven'
apply plugin: 'forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = '5.4'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def determinePatchVersion = {
    // get the name of the last tag
    def tagInfo = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = tagInfo
    }
    tagInfo = tagInfo.toString()

    if (!tagInfo.contains('-')) {
        return 0
    }
    return tagInfo.split("-")[1]
}

def majorVersion = '5'
def minorVersion = '4'
def patchVersion = determinePatchVersion()
def apiVersion = majorVersion + '.' + minorVersion
def fullVersion = apiVersion + '.' + patchVersion

minecraft {
    version = "${minecraftVersion}-${forgeVersion}"
    mappings = "stable_12"

    replaceIn 'LPForgeBootstrap.java'
    replace '@version@', fullVersion
}

repositories {
    flatDir {
        dirs("libs")
    }
}

dependencies {
    compileOnly group: 'org.realityforge.org.jetbrains.annotations', name: 'org.jetbrains.annotations', version: '1.7.0'
    compileOnly group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.6'
    compileOnly group: 'net.kyori', name: 'adventure-api', version: '4.11.0'
    compileOnly group: 'net.kyori', name: 'adventure-text-serializer-gson', version: '4.11.0'
    compileOnly group: 'net.kyori', name: 'adventure-text-serializer-plain', version: '4.11.0'
    compileOnly group: 'net.kyori', name: 'adventure-text-serializer-legacy', version: '4.11.0'
    compileOnly group: 'ninja.leaping.configurate', name: 'configurate-hocon', version: '3.7.1'
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.19.0'

    implementation files('api-5.4')
    implementation files('common-5.4-SNAPSHOT')
    compileOnly files('loader-utils-5.4-SNAPSHOT')
    compileOnly project(':forge-api')
    compileOnly project(':loader')
}

reobf {
    shadowJar {}
}

shadowJar {
    archiveName = "luckperms-forge.jarinjar"

    dependencies {
        include(dependency('me.lucko.luckperms:.*'))
    }

    relocate 'net.kyori.adventure', 'me.lucko.luckperms.lib.adventure'
    relocate 'net.kyori.event', 'me.lucko.luckperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'me.lucko.luckperms.lib.caffeine'
    relocate 'okio', 'me.lucko.luckperms.lib.okio'
    relocate 'okhttp3', 'me.lucko.luckperms.lib.okhttp3'
    relocate 'net.bytebuddy', 'me.lucko.luckperms.lib.bytebuddy'
    relocate 'me.lucko.commodore', 'me.lucko.luckperms.lib.commodore'
    relocate 'org.mariadb.jdbc', 'me.lucko.luckperms.lib.mariadb'
    relocate 'com.mysql', 'me.lucko.luckperms.lib.mysql'
    relocate 'org.postgresql', 'me.lucko.luckperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'me.lucko.luckperms.lib.hikari'
    relocate 'com.mongodb', 'me.lucko.luckperms.lib.mongodb'
    relocate 'org.bson', 'me.lucko.luckperms.lib.bson'
    relocate 'redis.clients.jedis', 'me.lucko.luckperms.lib.jedis'
    relocate 'io.nats.client', 'me.lucko.luckperms.lib.nats'
    relocate 'com.rabbitmq', 'me.lucko.luckperms.lib.rabbitmq'
    relocate 'org.apache.commons.pool2', 'me.lucko.luckperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'me.lucko.luckperms.lib.configurate'
    relocate 'org.yaml.snakeyaml', 'me.lucko.luckperms.lib.yaml'
}

artifacts {
    archives shadowJar
}

subprojects {
    group = 'me.lucko.luckperms'
    version = '5.4-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    project.ext.majorVersion = majorVersion
    project.ext.minorVersion = minorVersion
    project.ext.patchVersion = patchVersion
    project.ext.apiVersion = apiVersion
    project.ext.fullVersion = fullVersion

    repositories {
        mavenCentral()
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://repo.lucko.me/' }
        maven { url 'https://libraries.minecraft.net/' }
    }
}

if (hasProperty('buildScan')) {
    buildScan {
        licenseAgreementUrl = 'https://scans.gradle.com/terms-of-service'
        licenseAgree = 'yes'
    }
}
